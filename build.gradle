import java.nio.charset.Charset
import java.nio.charset.StandardCharsets

plugins {
	id 'java'
	id 'java-library'
	id 'maven-publish'
}

repositories {
	mavenCentral()
}

dependencies {
	
	/** provide javax.annotations.Nonnull annotations etc. */
	compileOnly "com.github.spotbugs:spotbugs-annotations:${lib_spotbugs_v}"
	
	testImplementation "org.junit.jupiter:junit-jupiter-api:${lib_junit_v}"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${lib_junit_v}"
	
	testImplementation project(":sekai-meta")
	
}

final JavaVersion proj_java = JavaVersion.VERSION_17
final Charset proj_source_encoding = StandardCharsets.UTF_8

group 'cc.sukazyo'
version projVersion

test {
	useJUnitPlatform()
}

java {
	
	sourceCompatibility proj_java
	targetCompatibility proj_java
	
	withSourcesJar()
	
}

tasks.withType(JavaCompile) {
	options.encoding = proj_source_encoding.name()
}

tasks.withType(Javadoc) {
	options.encoding = proj_source_encoding.name()
	options.docEncoding = proj_source_encoding.name()
	options.charSet = proj_source_encoding.name()
}

publishing {
	repositories{
		maven {
			name 'builds'
			url publishLocalArchiveRepoUrl
		}
		maven {
			name '-ws-'
			url publishMvnRepoUrl
			credentials {
				username publishMvnRepoUsername
				password publishMvnRepoPassword
			}
		}
	}
	publications {
		main (MavenPublication) {
			from components.java
			groupId = project.group
			artifactId = project.archivesBaseName
			version = project.version
		}
	}
}
